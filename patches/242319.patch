From afaa4411e0db764f09a7471719bd3b1aecefb38e Mon Sep 17 00:00:00 2001
From: SamLukeYes <samlukeyes123@gmail.com>
Date: Sat, 8 Jul 2023 17:36:52 +0800
Subject: [PATCH 1/2] pyalpm: init at 0.10.6

---
 .../python-modules/pyalpm/default.nix         | 38 +++++++++++++++++++
 pkgs/top-level/python-packages.nix            |  2 +
 2 files changed, 40 insertions(+)
 create mode 100644 pkgs/development/python-modules/pyalpm/default.nix

diff --git a/pkgs/development/python-modules/pyalpm/default.nix b/pkgs/development/python-modules/pyalpm/default.nix
new file mode 100644
index 0000000000000..b103c285a1f80
--- /dev/null
+++ b/pkgs/development/python-modules/pyalpm/default.nix
@@ -0,0 +1,38 @@
+{ lib
+, buildPythonPackage
+, fetchPypi
+, libarchive
+, pacman
+, pkgconfig
+, setuptools
+}:
+
+buildPythonPackage rec {
+  pname = "pyalpm";
+  version = "0.10.6";
+  format = "pyproject";
+
+  src = fetchPypi {
+    inherit pname version;
+    hash = "sha256-mebsc7jEa7EkZgE/Io+DHuDRjoq2ZLkaAcKjxA3gfH8=";
+  };
+
+  nativeBuildInputs = [
+    pkgconfig
+    setuptools
+  ];
+
+  buildInputs = [
+    libarchive
+    pacman
+  ];
+
+  pythonImportsCheck = [ "pyalpm" "pycman" ];
+
+  meta = with lib; {
+    description = "Libalpm bindings for Python 3";
+    homepage = "https://gitlab.archlinux.org/archlinux/pyalpm/";
+    license = licenses.gpl3Plus;
+    maintainers = with maintainers; [ samlukeyes123 ];
+  };
+}
diff --git a/pkgs/top-level/python-packages.nix b/pkgs/top-level/python-packages.nix
index 919ef010c48f7..82a5f7aa5f115 100644
--- a/pkgs/top-level/python-packages.nix
+++ b/pkgs/top-level/python-packages.nix
@@ -8409,6 +8409,8 @@ self: super: with self; {
 
   pyalgotrade = callPackage ../development/python-modules/pyalgotrade { };
 
+  pyalpm = callPackage ../development/python-modules/pyalpm { };
+
   pyamg = callPackage ../development/python-modules/pyamg { };
 
   pyaml = callPackage ../development/python-modules/pyaml { };

From 84c1aec7b6900c06594a282b6801009bcab8de1d Mon Sep 17 00:00:00 2001
From: SamLukeYes <samlukeyes123@gmail.com>
Date: Sun, 9 Jul 2023 11:38:56 +0800
Subject: [PATCH 2/2] namcap: init at 3.4.2

---
 .../package-management/namcap/default.nix     | 56 +++++++++++++++
 .../make-etc-pacman-conf-optional.patch       | 17 +++++
 .../namcap/python-module.nix                  | 70 +++++++++++++++++++
 pkgs/top-level/all-packages.nix               |  2 +
 4 files changed, 145 insertions(+)
 create mode 100644 pkgs/tools/package-management/namcap/default.nix
 create mode 100644 pkgs/tools/package-management/namcap/make-etc-pacman-conf-optional.patch
 create mode 100644 pkgs/tools/package-management/namcap/python-module.nix

diff --git a/pkgs/tools/package-management/namcap/default.nix b/pkgs/tools/package-management/namcap/default.nix
new file mode 100644
index 0000000000000..1ee9d4b71a5cc
--- /dev/null
+++ b/pkgs/tools/package-management/namcap/default.nix
@@ -0,0 +1,56 @@
+{ lib
+, stdenvNoCC
+, patsh
+, python3
+, gnugrep
+, zstd
+}:
+
+let
+  python = python3.override {
+    packageOverrides = final: prev: {
+      namcap = final.callPackage ./python-module.nix { inherit zstd; };
+    };
+  };
+  pythonEnv = python.withPackages (ps: with ps; [ namcap ]);
+  module = python.pkgs.namcap;
+in
+
+stdenvNoCC.mkDerivation {
+  inherit (module) src pname version;
+
+  dontBuild = true;
+
+  nativeBuildInputs = [ patsh ];
+
+  buildInputs = [
+    gnugrep
+  ];
+
+  postPatch = ''
+    patchShebangs scripts/namcap
+    substituteInPlace scripts/namcap \
+      --replace /usr/bin/env\ python3 ${pythonEnv}/bin/python3
+  '';
+
+  installPhase = ''
+    runHook preInstall
+    mkdir -p $out/bin
+    patsh scripts/namcap $out/bin/namcap
+    chmod +x $out/bin/namcap
+    ln -s ${module}/share $out/
+    runHook postInstall
+  '';
+
+  passthru = {
+    inherit python pythonEnv module;
+  };
+
+  meta = with lib; {
+    description = "Pacman package lint tool";
+    homepage = "https://gitlab.archlinux.org/pacman/namcap";
+    changelog = "https://gitlab.archlinux.org/pacman/namcap/-/blob/${module.version}/NEWS";
+    license = licenses.gpl2Plus;
+    maintainers = with maintainers; [ samlukeyes123 ];
+  };
+}
diff --git a/pkgs/tools/package-management/namcap/make-etc-pacman-conf-optional.patch b/pkgs/tools/package-management/namcap/make-etc-pacman-conf-optional.patch
new file mode 100644
index 0000000000000..497892597f82f
--- /dev/null
+++ b/pkgs/tools/package-management/namcap/make-etc-pacman-conf-optional.patch
@@ -0,0 +1,17 @@
+diff --git a/Namcap/package.py b/Namcap/package.py
+index b9302ae..c17df63 100644
+--- a/Namcap/package.py
++++ b/Namcap/package.py
+@@ -16,7 +16,11 @@ _pyalpm_version_tuple = tuple(int(n) for n in pyalpm.version().split("."))
+ if _pyalpm_version_tuple < (0, 5):
+     raise DeprecationWarning("pyalpm versions <0.5 are no longer supported")
+ 
+-pyalpm_handle = pycman.config.init_with_config("/etc/pacman.conf")
++if os.path.isfile("/etc/pacman.conf"):
++    pyalpm_handle = pycman.config.init_with_config("/etc/pacman.conf")
++else:
++    from warnings import warn
++    warn("/etc/pacman.conf doesn't exist. We'll not be able to load pacman repositories.")
+ 
+ DEPENDS_RE = re.compile(r"([^<>=:]+)([<>]?=.*)?(: .*)?")
+ SODEPENDS_RE = re.compile(r"([^:]+)(: .*)?")
diff --git a/pkgs/tools/package-management/namcap/python-module.nix b/pkgs/tools/package-management/namcap/python-module.nix
new file mode 100644
index 0000000000000..ac45655f1f882
--- /dev/null
+++ b/pkgs/tools/package-management/namcap/python-module.nix
@@ -0,0 +1,70 @@
+{ fetchFromGitLab
+, fetchzip
+, coreutils
+, pacman
+, python
+, runtimeShell
+, zstd
+}:
+
+let
+  licenses = fetchzip {
+    url = "https://geo.mirror.pkgbuild.com/core/os/x86_64/licenses-20220125-2-any.pkg.tar.zst";
+    hash = "sha256-8HEn+pFpxPUi6hyM2wvWcdImIK1Cgjn1I8PYiguYR5s=";
+    nativeBuildInputs = [ zstd ];
+    stripRoot = false;
+  };
+in
+
+python.pkgs.buildPythonPackage rec {
+  pname = "namcap";
+  version = "3.4.2";
+  format = "pyproject";
+
+  src = fetchFromGitLab {
+    domain = "gitlab.archlinux.org";
+    owner = "pacman";
+    repo = "namcap";
+    rev = version;
+    hash = "sha256-SxOIUkzJdvvnq15FEMfz6WIXl501sq56t3cB7Jxft5Q=";
+  };
+
+  patches = [
+    ./make-etc-pacman-conf-optional.patch
+  ];
+
+  postPatch = ''
+    substituteInPlace scripts/parsepkgbuild \
+      --replace "/bin/bash" "${runtimeShell}" \
+      --replace "/usr/bin/env" "${coreutils}/bin/env" \
+      --replace "/etc/makepkg.conf" "${pacman}/etc/makepkg.conf" \
+      --replace "''${PARSE_PKGBUILD_PATH:-/usr/share/namcap}" "$out/share/namcap"
+    substituteInPlace Namcap/package.py \
+      --replace '"parsepkgbuild"' "\"$out/bin/parsepkgbuild\""
+    substituteInPlace Namcap/tags.py \
+      --replace "/usr/share/namcap" "$out/share/namcap"
+    substituteInPlace Namcap/rules/licensepkg.py \
+      --replace "/usr/share/licenses" "${licenses}/usr/share/licenses"
+  '';
+
+  nativeBuildInputs = with python.pkgs; [
+    setuptools
+  ];
+
+  propagatedBuildInputs = with python.pkgs; [
+    pyalpm
+    pyelftools
+  ];
+
+  pythonImportsCheck = [ "Namcap" ];
+
+  postInstall = ''
+    USR=$out/${python.sitePackages}/usr
+    mv $USR/* $out
+    rmdir $USR
+  '';
+
+  passthru = {
+    inherit licenses;
+  };
+}
diff --git a/pkgs/top-level/all-packages.nix b/pkgs/top-level/all-packages.nix
index 146c63597b6d3..2ac35545ef785 100644
--- a/pkgs/top-level/all-packages.nix
+++ b/pkgs/top-level/all-packages.nix
@@ -11270,6 +11270,8 @@ with pkgs;
 
   pacman = callPackage ../tools/package-management/pacman { };
 
+  namcap = callPackage ../tools/package-management/namcap { };
+
   paco = callPackage ../development/compilers/paco { };
 
   padthv1 = libsForQt5.callPackage ../applications/audio/padthv1 { };
